// Source: http://tools.android.com/tech-docs/new-build-system/user-guide#TOC-Introduction

buildscript         // This is the block required to setup gradle itself to carry out the building. It uses maven to download necessary files for gradle to perform its tasks.
        {
            repositories
                    {
                        mavenCentral()
                    }

            dependencies
                    {
                        classpath 'com.android.tools.build:gradle:0.6.+'
                    }
        }

apply plugin: 'android'         // We declare the project this build.gradle file builds to be an Android project

android                         // Configuration for the android project
    {
        compileSdkVersion 19                // This may be left out and will be picked up from AndroidManifest.xml
        buildToolsVersion "19.0.0"          // This is the version of the build tools found in ~/applications/android-sdk/build-tools/ folder

        defaultConfig
                {
                    minSdkVersion 10
                    targetSdkVersion 19
                }

        sourceSets                          // Here we define where we can find the various files necessary to construct a functioning Android project
                {
                    main
                            {
                                manifest.srcFile 'AndroidManifest.xml'
                                java.srcDirs = ['src']                          // Since this project is legacy from IntelliJ most of the sources are in the src/ subfolder
                                resources.srcDirs = ['src']
                                aidl.srcDirs = ['src']
                                renderscript.srcDirs = ['src']
                                res.srcDirs = ['res']                           // Explicitly declare 'res' and 'assets' folder
                                assets.srcDirs = ['assets']
                            }
                }


    }


repositories                    // Here we declare the repositories to use for getting the dependencies defined below
        {
            mavenCentral()
        }


dependencies                    // Dependencies of the project being built (using this build.gradle file mainly "fetchheaders-android"
        {
            compile 'com.android.support:support-v4:18.0.+'         // This library is fetched based on the information contained in the Android Support Repo which we downloaded using the SDK Manager
            compile 'com.android.support:appcompat-v7:18.0.+'

            compile group: 'com.sun.mail', name: 'javax.mail', version: '1.5.1'         // This dependency is downloaded straight from Maven Central and doesn't need to be downloaded ourselves. It is kept in the build/ subfolder that gradle builds
                                                                                        // To get this information enter the package name in: http://gradleplease.appspot.com/

            compile project(':AndroidSVG')          // This is an Android Library project defined in /AndroidSVG subfolder
                                                    // The : before the name indicates that it is found in the ./AndroidSVG folder relative to the settings.gradle.
                                                    // Library projects MUST be included in settings.gradle. They MUST implement their own build.gradle file where they use the
                                                    // 'android-library' plugin rather than the 'android' plugin
        }


task wrapper(type: Wrapper) {               // Defines a "Wrapper" task which generates the files required for Gradle Wrapper
    gradleVersion = '1.8'
}


/*
 * NOTES:
 *
 * Is known to work on gradle-1.8 and NOT to work on gradle-1.10
 *
 * For the gradle build to work one needs to define the sdk.dir=<absolute android sdk path without quotes> in the local.properties files.
 */